// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution ixxaccccccdddjjj
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS: // 0000000000010101
    // Put your code here:
    Not(in=instruction[15], out=ABitSet);
    Or(a=ABitSet, b=instruction[5], out=loadARegister);
    Mux16(a=ALUout, b=instruction, sel=ABitSet, out=muxInstruction);
    ARegister(in=muxInstruction, load=loadARegister, out=ARegisterOut, out[0..14]=addressM);
    And(a=instruction[15], b=instruction[4], out=loadDRegister);
    DRegister(in=ALUout, load=loadDRegister, out=DRegisterOut);
    Mux16(a=ARegisterOut, b=inM, sel=instruction[12], out=muxARegisterAndInM);

    ALU(x=DRegisterOut, y=muxARegisterAndInM, zx=instruction[11], 
      nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7],
        no=instruction[6], out=ALUout, out=outM, zr=ALUzr, ng=ALUng);

    And(a=instruction[3], b=instruction[15], out=writeM);

    // JUMP instructions
    Not(in=ALUzr, out=ALUnonzr);
    Not(in=ALUng, out=ALUpos);

    And(a=ALUnonzr, b=ALUpos, out=outGreaterZero); // out > 0
    // ALUzr // out = 0
    Or(a=ALUzr, b=ALUpos, out=outGreaterEQZero); // out >= 0
    And(a=ALUnonzr, b=ALUng, out=outSmallerZero); // out < 0
    // ALUnonzr // out != 0
    Or(a=ALUzr, b=ALUng, out=outSmallerEQZero); // out <= 0

    Mux(a=false, b=outSmallerZero, sel=instruction[2], out=mux1);
    Mux(a=outGreaterZero, b=ALUnonzr, sel=instruction[2], out=mux2);
    Mux(a=ALUzr, b=outSmallerEQZero, sel=instruction[2], out=mux3);
    Mux(a=outGreaterEQZero, b=true, sel=instruction[2], out=mux4);

    Mux(a=mux1, b=mux3, sel=instruction[1], out=mux5);
    Mux(a=mux2, b=mux4, sel=instruction[1], out=mux6);

    Mux(a=mux5, b=mux6, sel=instruction[0], out=shouldJump);

    And(a=shouldJump, b=instruction[15], out=jump);
    Not(in=jump, out=notJump);
    PC(in=ARegisterOut, load=jump, inc=notJump, reset=reset, out[0..14]=pc);
}